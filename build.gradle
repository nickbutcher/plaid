/*
 * Copyright 2015 Google LLC.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler
    ext.versions = [
            'compileSdk'         : 29,
            'minSdk'             : 23,
            'targetSdk'          : 29,
            'appcompat'          : '1.1.0-rc01',
            'androidx'           : '1.0.0',
            'androidxCollection' : '1.0.0',
            'androidxArch'       : '2.0.0',
            'constraintLayout'   : '2.0.0-alpha2',
            'coreKtx'            : '1.2.0-alpha03',
            'coroutines'         : '1.3.0',
            'crashlytics'        : '2.10.1',
            'dagger'             : '2.23.2',
            'espresso'           : '3.1.0-beta02',
            'extJunit'           : '1.1.0',
            'fabric'             : '1.28.0',
            'firebase'           : '17.0.0',
            'glide'              : '4.9.0',
            'googleServices'     : '4.3.0',
            'gson'               : '2.8.5',
            'jsoup'              : '1.11.3',
            'junit'              : '4.12',
            'kotlin'             : '1.3.50',
            'ktlint'             : '0.36.0',
            'legacyCoreUtils'    : '1.0.0',
            'lifecycle'          : '2.2.0-alpha03',
            'material'           : '1.1.0-alpha05',
            'mockito'            : '2.23.0',
            'mockito_kotlin'     : '2.0.0-RC3',
            'okhttp'             : '4.0.0',
            'retrofit'           : '2.6.0',
            'room'               : '2.2.1',
            'supportLibrary'     : '28.0.0',
            'test_rules'         : '1.1.0-beta02',
            'test_runner'        : '1.1.0-beta02',
            'ui_automator'       : '2.2.0-beta02',
            'viewPager2'         : '1.0.0'
    ]
    ext.names = [
            'applicationId': 'io.plaidapp'
    ]

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "io.fabric.tools:gradle:${versions.fabric}"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.26.1'
}

ext {
    // query git for the commit count to automate versioning.
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt')
        }
        groovyGradle {
            // same as groovy, but for .gradle (defaults to '*.gradle')
            target '**/*.gradle'
            paddedCell() // Avoid cyclic ambiguities
            // the Groovy Eclipse formatter extends the Java Eclipse formatter,
            // so it formats Java files by default (unless `excludeJava` is used).
            greclipse().configFile(project.rootProject.file('scripts/greclipse.properties'))
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt'), '(buildscript|apply)'
        }
    }

    if (project.hasProperty( "kapt" )) {
        kapt {
            useBuildCache = true
        }
    }
}
