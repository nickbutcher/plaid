/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    ext.versions = [
            'compileSdk'        : 28,
            'minSdk'            : 23,
            'targetSdk'         : 28,
            'constraintLayout'  : '1.1.2',
            'coreKtx'           : '0.3',
            'coroutines'        : '0.22.5',
            'crashlytics'       : '2.9.4',
            'espresso'          : '3.0.2',
            'fabric'            : '1.25.4',
            'firebase'          : '16.0.1',
            'glide'             : '4.7.1',
            'googleServices'    : '4.0.1',
            'gson'              : '2.8.5',
            'jsoup'             : '1.11.3',
            'junit'             : '4.12',
            'kotlin'            : '1.2.51',
            'ktlint'            : '0.24.0',
            'lifecycle'         : '1.1.1',
            'mockito'           : '2.19.0',
            'mockito_kotlin'    : '1.6.0',
            'okhttp'            : '3.10.0',
            'retrofit'          : '2.4.0',
            'retrofitCoroutines': '1.0.0',
            'supportLibrary'    : '27.1.1',
            'test_rules'        : '1.0.2',
            'test_runner'       : '1.0.2'
    ]
    ext.names = [
            'applicationId': 'io.plaidapp'
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "io.fabric.tools:gradle:${versions.fabric}"
    }

}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.13.0'
}

ext {
    // query git for the SHA, Tag and commit count. Use these to automate versioning.
    gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt')
        }
    }
}
