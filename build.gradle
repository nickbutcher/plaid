/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    ext.versions = [
            'compileSdk'        : 28,
            'minSdk'            : 23,
            'targetSdk'         : 28,
            'androidx'          : '1.0.0',
            'constraintLayout'  : '2.0.0-alpha2',
            'coreKtx'           : '1.0.0',
            'coroutines'        : '0.26.1-eap13',
            'crashlytics'       : '2.9.4',
            'dagger'            : '2.16',
            'espresso'          : '3.1.0-beta02',
            'fabric'            : '1.25.4',
            'firebase'          : '16.0.1',
            'glide'             : '4.8.0',
            'googleServices'    : '4.0.1',
            'gson'              : '2.8.5',
            'jsoup'             : '1.11.3',
            'junit'             : '4.12',
            'kotlin'            : '1.3.0-rc-190',
            'ktlint'            : '0.24.0',
            'legacyCoreUtils'   : '1.0.0',
            'lifecycle'         : '2.0.0',
            'material'          : '1.0.0',
            'mockito'           : '2.23.0',
            'mockito_kotlin'    : '2.0.0-RC3',
            'okhttp'            : '3.10.0',
            'retrofit'          : '2.4.0',
            'retrofitCoroutines': '0.9.2',
            'room'              : '2.1.0-alpha01',
            'test_rules'        : '1.1.0-beta02',
            'test_runner'       : '1.1.0-beta02',
            'ui_automator'      : '2.2.0-beta02'
    ]
    ext.names = [
            'applicationId': 'io.plaidapp'
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha08'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "io.fabric.tools:gradle:${versions.fabric}"
    }

}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.14.0'
}

ext {
    // query git for the SHA, Tag and commit count. Use these to automate versioning.
    gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt')
        }
    }
}
