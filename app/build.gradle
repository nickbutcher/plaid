/*
 * Copyright 2015 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId names.applicationId
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.gitCommitCount
        versionName rootProject.gitTag
        archivesBaseName = "plaid"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfig "en"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
         manifestPlaceholders += [
                 crashlyticsEnabled: false
         ]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders += [crashlyticsEnabled: true]
            buildConfigField "boolean", "DESIGNER_NEWS_V2", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dynamicFeatures = [':about', ':designernews', ':dribbble']
}

repositories {
    google()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    api project(':core')
    api project(':bypass')
    implementation project(':search')
    implementation "com.crashlytics.sdk.android:crashlytics:${versions.chrashlytics}"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

// Must be applied after dependencies. See https://stackoverflow.com/a/38018985
apply plugin: 'com.google.gms.google-services'
